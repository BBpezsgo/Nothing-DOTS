using "lib/turret.bbc";
using "lib/move.bbc";
using "lib/api.bbc";
using "lib/protocol.bbc";

[UnitCommand(1, "Build at")]
struct CommandBuildAt
{
    [Context("position3")] float3 position;
}

[UnitCommand(2, "Move to")]
struct CommandMoveTo
{
    [Context("position3")] float3 position;
}

const int MaxCommandDataSize = 8;

void Receive()
{
    u8[MaxPacketLength] v;
    float3 direction;

    while (1)
    {
        i32 receivedLength = receive(&v, MaxPacketLength, &direction);
        if (!receivedLength) { break; }

        if (v[0] == (u8)REQUEST_HEADER &&
            receivedLength == sizeof(VerificationRequestPacket))
        {
            VerificationRequestPacket* packet = &v as VerificationRequestPacket*;

            VerificationResponsePacket responsePacket = new VerificationResponsePacket;
            responsePacket.header = (u8)RESPONSE_HEADER;
            responsePacket.position = packet.position;

            if (distancesqr(packet.position, GPS.position) < DistanceThresholdSq)
            {
                debug(packet.position, CYAN);

                send(&responsePacket as u8[]*, sizeof(VerificationResponsePacket), &direction, 0.1f);
            }
        }
    }
}

float3 currentDestination = new float3;
float3 currentBuildAt = new float3;
float3 targetedPosition = new float3;
f32 turretLastUseTime = 0f;

while (1)
{
    Receive();

    byte[MaxCommandDataSize] _command;

    int commandId = dequeue_command(&_command);

    if (commandId == 1)
    {
        CommandMoveTo* command = &_command as CommandMoveTo*;
        currentBuildAt = command.position;
        currentDestination = new float3;
    }
    else if (commandId == 2)
    {
        CommandMoveTo* command = &_command as CommandMoveTo*;
        currentBuildAt = new float3;
        currentDestination = command.position;
    }

    if (currentDestination.x != 0f &&
        currentDestination.z != 0f)
    {
        move_to(currentDestination.x, currentDestination.z);
    }

    if (currentBuildAt.x != 0f &&
        currentBuildAt.z != 0f)
    {
        turretLastUseTime = time();
        float d = distancesqr(GPS.position, currentBuildAt);
        if (d < 3f * 3f)
        {
            stop_moving();

            shootat_simple(currentBuildAt, &targetedPosition);
            debug(currentBuildAt, WHITE);
        }
        else
        {
            move_to(currentBuildAt.x, currentBuildAt.z);
        }
    }
    else if (time() - turretLastUseTime > 5f)
    {
        CombatTurret.target_rotation = 0f;
        CombatTurret.target_angle = 0f;
    }
}
