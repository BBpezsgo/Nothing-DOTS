using "turret.bbc";
using "move.bbc";
using "api.bbc";

const f32 DISTANCE_THRESHOLD = 5f * 5f;

const u8 REQUEST_HEADER = 0b_11110000;
const u8 RESPONSE_HEADER = 0b_01111000;

void Receive()
{
    u8[3] v;
    f32 direction;

    while (1)
    {
        i32 receivedLength = receive(&v, 3, &direction);
        if (!receivedLength) { break; }

        if (v[0] == (u8)REQUEST_HEADER && receivedLength == 3)
        {
            byte2 p;
            float2 pos;
            p.x = v[1] as i8;
            p.y = v[2] as i8;
            ToFloat2(&p, &pos);

            if (DistanceSqr(pos, GPS.Position) < 2f * 2f)
            {
                debug(pos, CYAN);

                v[0] = (u8)RESPONSE_HEADER;
                v[1] = p.x as u8;
                v[2] = p.y as u8;
                send(&v, 3, direction, 0.1f);
                // PrintLine("Send response ...");
            }
        }
        else if (v[0] == (u8)RESPONSE_HEADER && receivedLength == 3)
        {
            float2 pos;
            byte2 p;
            p.x = v[1] as i8;
            p.y = v[2] as i8;
            ToFloat2(&p, &pos);
            ToLocal(&pos);
            ToByte2(&pos, &p);
            AddUnitScore(&units, p, 1);

            ldebug(pos, GREEN);

            // PrintLine("Object identified ...");
        }
    }
}

int GetUnit(List<Unit>* units, byte2 unit)
{
    for (int i = 0; i < units.Length(); i++)
    {
        f32 distanceSqr = DistanceSqr(units.Raw()[i].Position, unit);
        if (distanceSqr < DISTANCE_THRESHOLD)
        {
            return i;
        }
    }
    return -1;
}

void RecordUnit(List<Unit>* units, Unit unit)
{
    int recorded = GetUnit(units, unit.Position);
    if (recorded != -1) { return; }
    if (units.Length() >= 8)
    {
        PrintLine("Too many units recorded");
        units.Raw()[0] = unit;
    }
    else
    {
        units.Add(unit);
    }
}

void RemoveUnit(List<Unit>* units, byte2 unit)
{
    u8 isNotDone = true;
    for (int i = 0; isNotDone && i < units.Length(); i++)
    {
        f32 distanceSqr = DistanceSqr(units.Raw()[i].Position, unit);
        if (distanceSqr < DISTANCE_THRESHOLD)
        {
            isNotDone = false;
            units.Remove(i);
        }
    }
}

void AddUnitScore(List<Unit>* units, byte2 unit, int score)
{
    int recorded = GetUnit(units, unit);
    if (recorded == -1)
    {
        Unit newUnit = new Unit;
        newUnit.Recorded = time();
        newUnit.Expires = time() + 10f;
        newUnit.Score = score;
        newUnit.Position = unit;
        RecordUnit(units, newUnit);
    }
    else
    {
        Unit* recordedUnit = &units.Raw()[recorded];
        recordedUnit.Score += score;
    }
}

void PerformRadar()
{
    // PrintLine("Radar ...");
    f32 radarDistance = radar();
    if (radarDistance > 0f &&
        Abs(prevRadarDistance - radarDistance) > .5f)
    {
        prevRadarDistance = radarDistance;

        float2 point;
        point.x = Cos(Radar.RadarDirection);
        point.y = Sin(Radar.RadarDirection);
        point.x *= radarDistance;
        point.y *= radarDistance;

        byte2 pLocal;
        ToByte2(&point, &pLocal);

        int recorded = GetUnit(&units, pLocal);
        if (recorded == -1 ||
            (&units).Raw()[recorded].Score <= 0)
        {
            // PrintLine("Identifying object ...");

            float2 pointGlobal = point;
            ToGlobal(&pointGlobal);
            byte2 p;
            ToByte2(&pointGlobal, &p);

            u8[3] v;
            v[0] = (u8)REQUEST_HEADER;
            v[1] = p.x as u8;
            v[2] = p.y as u8;
            send(&v, 3, Radar.RadarDirection, Asin(1.2f / radarDistance));

            ldebug(point, BLUE);

            AddUnitScore(&units, pLocal, 0);
        }
    }
    if (radarDistance == 0f)
    {
        prevRadarDistance = 0f;
    }
    Radar.RadarDirection = (Radar.RadarDirection + 0.1f) % TAU;
}

struct Unit
{
    byte2 Position;
    f32 Recorded;
    f32 Expires;
    i32 Score;
}

f32 prevRadarDistance = 0f;
float2 targetedPosition = new float2;
float2 currentTarget = new float2;
List<Unit> units = new List<Unit>(4);

while (1)
{
    Receive();

    PerformRadar();

    currentTarget = new float2;

    for (int i = (&units).Length() - 1; i >= 0; i--)
    {
        Unit* unit = &((&units).Raw())[i];
        if (time() >= unit.Expires)
        {
            (&units).Remove(i);
        }
        else if (unit.Score > 0)
        {
            float2 p1;
            byte2 p2 = unit.Position;
            ToFloat2(&p2, &p1);
            ldebug(p1, GREEN);
        }
        else if (unit.Score <= 0 &&
                 time() >= unit.Recorded + 2f)
        {
            float2 p1;
            byte2 p2 = unit.Position;
            ToFloat2(&p2, &p1);
            ldebug(p1, RED);
            currentTarget = p1;
        }
        else
        {
            float2 p1;
            byte2 p2 = unit.Position;
            ToFloat2(&p2, &p1);
            ldebug(p1, YELLOW);
        }
    }

    if (currentTarget.x != 0f &&
        currentTarget.x != 0f)
    {
        ShootAt(currentTarget, &targetedPosition);
    }
}
