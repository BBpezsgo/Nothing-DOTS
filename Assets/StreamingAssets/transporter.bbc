using "move.bbc";
using "api.bbc";

[UnitCommand(1, "In")]
struct CommandIn
{
    [Context("position")] float2 position;
}

[UnitCommand(2, "Out")]
struct CommandOut
{
    [Context("position")] float2 position;
}

[UnitCommand(3, "Move to")]
struct CommandMoveTo
{
    [Context("position")] float2 position;
}

[UnitCommand(4, "Set Source")]
struct CommandSetSource
{
    [Context("position")] float2 position;
}

[UnitCommand(5, "Set Destination")]
struct CommandSetDestination
{
    [Context("position")] float2 position;
}

const int MaxCommandDataSize = 8;

void Receive()
{
    u8[MaxPacketLength] v;
    float3 direction;

    while (1)
    {
        i32 receivedLength = wreceive(&v, MaxPacketLength, &direction);
        if (!receivedLength) { break; }

        if (v[0] == (u8)REQUEST_HEADER &&
            receivedLength == sizeof(VerificationRequestPacket))
        {
            VerificationRequestPacket* packet = &v as VerificationRequestPacket*;

            if (distancesqr(packet.position, GPS.position) < DistanceThresholdSq)
            {
                printline(" M");
                VerificationResponsePacket responsePacket = new VerificationResponsePacket;
                responsePacket.header = (u8)RESPONSE_HEADER;
                responsePacket.position = packet.position;
                debug(packet.position, CYAN);
                wsend(&responsePacket as u8[]*, sizeof(VerificationResponsePacket), &direction, 0.1f);
            }
        }
    }
}

float2 currentDestination = new float2;
u8 currentDirectionAtDestination = (u8)0;

float2 _loadSource = new float2;
float2 _loadDestination = new float2;
int _state = 0;

while (1)
{
    Receive();

    byte[MaxCommandDataSize] _command;

    int commandId = dequeue_command(&_command);

    if (commandId == 1)
    {
        CommandIn* command = &_command as CommandIn*;
        currentDirectionAtDestination = (u8)2;
        currentDestination = command.position;
    }
    else if (commandId == 2)
    {
        CommandOut* command = &_command as CommandOut*;
        currentDirectionAtDestination = (u8)1;
        currentDestination = command.position;
    }
    else if (commandId == 3)
    {
        CommandMoveTo* command = &_command as CommandMoveTo*;
        currentDirectionAtDestination = (u8)0;
        currentDestination = command.position;
    }
    else if (commandId == 4)
    {
        CommandSetSource* command = &_command as CommandSetSource*;
        _loadSource = command.position;
        currentDestination = new float2;
    }
    else if (commandId == 5)
    {
        CommandSetDestination* command = &_command as CommandSetDestination*;
        _loadDestination = command.position;
        currentDestination = new float2;
    }

    if (currentDestination.x != 0f &&
        currentDestination.y != 0f)
    {
        float d = distancesqr(GPS.position, currentDestination);
        if (d < 2f * 2f)
        {
            stop_moving();
            Transporter.load_direction = currentDirectionAtDestination;
        }
        else
        {
            move_to(currentDestination.x, currentDestination.y);
        }
    }
    else if (_loadSource.x != 0f &&
             _loadSource.y != 0f &&
             _loadDestination.x != 0f &&
             _loadDestination.y != 0f)
    {
        if (_state == 0)
        {
            float d = distancesqr(GPS.position, _loadSource);
            if (d < 2f * 2f)
            {
                _state = 1;
            }
            else
            {
                move_to(_loadSource.x, _loadSource.y);
            }
        }
        elseif (_state == 1)
        {
            stop_moving();
            Transporter.load_direction = (u8)2;
            if (Transporter.current_load > 10)
            {
                _state = 2;
            }
        }
        elseif (_state == 2)
        {
            float d = distancesqr(GPS.position, _loadDestination);
            if (d < 2f * 2f)
            {
                _state = 3;
            }
            else
            {
                move_to(_loadDestination.x, _loadDestination.y);
            }
        }
        elseif (_state == 3)
        {
            stop_moving();
            Transporter.load_direction = (u8)1;
            if (Transporter.current_load <= 0)
            {
                _state = 0;
            }
        }
    }
}
