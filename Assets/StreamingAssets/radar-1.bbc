using "lib/api.bbc";
using "lib/protocol.bbc";

void Receive()
{
    u8[MaxPacketLength] v;
    float3 direction;

    while (1)
    {
        i32 receivedLength = receive(&v, MaxPacketLength, &direction);
        if (!receivedLength) { break; }

        if (v[0] == (u8)REQUEST_HEADER &&
            receivedLength == sizeof(VerificationRequestPacket))
        {
            VerificationRequestPacket* packet = &v as VerificationRequestPacket*;

            if (Logs)
            {
                print("I-REQ ");
                print(packet.position);
                printline();
            }

            if (distancesqr(packet.position, GPS.position) < DistanceThresholdSq)
            {
                if (Logs) print(" M");
                VerificationResponsePacket responsePacket = new VerificationResponsePacket;
                responsePacket.header = (u8)RESPONSE_HEADER;
                responsePacket.position = GPS.position;
                if (Debug) debug(packet.position, CYAN);
                send(&responsePacket as u8[]*, sizeof(VerificationResponsePacket), &direction);
            }
        }
    }
}

void PerformRadar()
{
    //ldebug(new float3(cos(Radar.direction), 0f, sin(Radar.direction)) * 10f, CYAN);
    float3 radarPoint = radar();
    if (!empty(radarPoint))
    {
        toglobal(&radarPoint);

        RadarPointPacket radarPointPacket;
        radarPointPacket.header = RADAR_POINT;
        radarPointPacket.position = radarPoint;
        send(&radarPointPacket as u8[]*, sizeof(RadarPointPacket));
    }
    Radar.direction = (Radar.direction + 0.05f) % TAU;
}

printline("Started ...");

while (1)
{
    Receive();

    PerformRadar();
}
