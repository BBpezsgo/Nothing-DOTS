using "turret.bbc";
using "move.bbc";
using "api.bbc";
using "protocol.bbc";

[UnitCommand(1, "Attack at")]
struct CommandAttackAt
{
    [Context("position")] float2 position;
}

[UnitCommand(2, "Move to")]
struct CommandMoveTo
{
    [Context("position")] float2 position;
}

const int MaxCommandDataSize = 8;

void Receive()
{
    u8[MaxPacketLength] v;
    f32 direction;

    while (1)
    {
        i32 receivedLength = receive(&v, MaxPacketLength, &direction);
        if (!receivedLength) { break; }

        if (v[0] == (u8)REQUEST_HEADER &&
            receivedLength == sizeof(VerificationRequestPacket))
        {
            VerificationRequestPacket* packet = &v as VerificationRequestPacket*;

            VerificationResponsePacket responsePacket = new VerificationResponsePacket;
            responsePacket.Header = (u8)RESPONSE_HEADER;

            if (DistanceSqr(packet.Position, GPS.Position) < DistanceThresholdSq)
            {
                responsePacket.Position = GPS.Position;
                debug(packet.Position, CYAN);
                send(&responsePacket as u8[]*, sizeof(VerificationResponsePacket), direction, 0.1f);
            }
            else
            {
                for (int i = 0; i < (&allies).Length(); i++)
                {
                    f32 distanceSqr = DistanceSqr((&allies).Raw()[i].Position, packet.Position);
                    if (distanceSqr < DistanceThresholdSq)
                    {
                        IndirectVerificationResponsePacket indirectResponsePacket = new IndirectVerificationResponsePacket;
                        indirectResponsePacket.Header = (u8)INDIRECT_RESPONSE_HEADER;
                        indirectResponsePacket.Position = (&allies).Raw()[i].Position;
                        indirectResponsePacket.Expires = (&allies).Raw()[i].Expires;
                        send(&indirectResponsePacket as u8[]*, sizeof(IndirectVerificationResponsePacket), direction, 0.1f);
                        return;
                    }
                }
            }
        }
        else if (v[0] == (u8)RESPONSE_HEADER &&
                 receivedLength == sizeof(VerificationResponsePacket))
        {
            VerificationResponsePacket* packet = &v as VerificationResponsePacket*;

            Unit unit;
            unit.Position = packet.Position;
            unit.Recorded = time();
            unit.Expires = time() + 10f;
            unit.VerificationSentAt = time();
            unit.PositionRecords = 1;
            RemoveUnitAt(&enemies, unit.Position);
            UpdateUnit(&allies, unit);
        }
        else if (v[0] == (u8)ENEMY_HEADER &&
                 receivedLength == sizeof(EnemyNotificationPacket))
        {
            EnemyNotificationPacket* packet = &v as EnemyNotificationPacket*;

            Unit unit;
            unit.Position = packet.Position;
            unit.Recorded = 0f;
            unit.Expires = time() + 5f;
            unit.VerificationSentAt = time();
            unit.PositionRecords = 1;
            UpdateUnit(&enemies, unit);
        }
        else if (v[0] == (u8)INDIRECT_RESPONSE_HEADER &&
                 receivedLength == sizeof(IndirectVerificationResponsePacket))
        {
            IndirectVerificationResponsePacket* packet = &v as IndirectVerificationResponsePacket*;

            Unit unit;
            unit.Position = packet.Position;
            unit.Recorded = time();
            unit.Expires = packet.Expires;
            unit.VerificationSentAt = time();
            unit.PositionRecords = 1;
            RemoveUnitAt(&enemies, unit.Position);
            UpdateUnit(&allies, unit);
        }
    }
}

int UnitIndexAt(List<Unit>* units, float2 position)
{
    for (int i = 0; i < units.Length(); i++)
    {
        if (DistanceSqr(units.Raw()[i].Position, position) < DistanceThresholdSq)
        { return i; }
    }
    return -1;
}

int UnitIndexAt(List<Unit>* units, float2 position, float* distanceSqr)
{
    for (int i = 0; i < units.Length(); i++)
    {
        *distanceSqr = DistanceSqr(units.Raw()[i].Position, position);
        if ((*distanceSqr) < DistanceThresholdSq)
        { return i; }
    }
    return -1;
}

void UpdateUnit(List<Unit>* units, Unit unit)
{
    float distanceSqr = 0f;
    int existing = UnitIndexAt(units, unit.Position, &distanceSqr);
    if (existing == -1)
    {
        if (units.Length() >= 4)
        {
            PrintLine("Too many units");
            units.Raw()[0] = unit;
        }
        else
        {
            units.Add(unit);
        }
    }
    elseif (distanceSqr < 2f)
    {
        if (units.Raw()[existing].PositionRecords > 20)
        {
            unit.Position.x = unit.Position.x;
            unit.Position.y = unit.Position.y;
            unit.PositionRecords = 1;
            units[existing] = unit;
        }
        else
        {
            unit.Position.x = (units.Raw()[existing].Position.x * unit.PositionRecords + unit.Position.x) / (float)(unit.PositionRecords + 1);
            unit.Position.y = (units.Raw()[existing].Position.y * unit.PositionRecords + unit.Position.y) / (float)(unit.PositionRecords + 1);
            unit.PositionRecords++;
            units[existing] = unit;
        }
    }
    else
    {
        unit.Position.x = unit.Position.x;
        unit.Position.y = unit.Position.y;
        units[existing] = unit;
    }
}

void RemoveUnitAt(List<Unit>* units, float2 unit)
{
    u8 isNotDone = true;
    for (int i = 0; isNotDone && i < units.Length(); i++)
    {
        f32 distanceSqr = DistanceSqr(units.Raw()[i].Position, unit);
        if (distanceSqr < DistanceThresholdSq)
        {
            isNotDone = false;
            units.Remove(i);
        }
    }
}

struct Unit
{
    float2 Position;
    f32 Recorded;
    f32 Expires;
    f32 VerificationSentAt;
    u8 PositionRecords;
}

float2 targetedPosition = new float2;
float2 currentTarget = new float2;
List<Unit> enemies = new List<Unit>(4);
List<Unit> allies = new List<Unit>(4);
float2 currentDestination = new float2;
u8 shouldShoot = false;
f32 turretLastUseTime = 0f;

PrintLine("Started ...");

while (1)
{
    Receive();

    currentTarget = new float2;
    shouldShoot = false;

    for (int i = (&enemies).Length() - 1; i >= 0; i--)
    {
        if (time() >= (&enemies).Raw()[i].Expires)
        {
            (&enemies).Remove(i);
        }
        else
        {
            float2 p = (&enemies).Raw()[i].Position;
            ToLocal(&p);

            float2 norm = Normalised(p);
            Radar.RadarDirection = Atan2(norm.y, norm.x);
            f32 radarDistance = radar();
            bool destroyed = radarDistance <= 0f;
            if (!destroyed)
            {
                float2 p2 = norm * radarDistance;
                if (DistanceSqr(p, p2) > DistanceThresholdSq)
                {
                    destroyed = true;
                }
            }

            if (destroyed)
            {
                (&enemies).Remove(i);
            }
            elseif (time() >= (&enemies).Raw()[i].Recorded + 4f)
            {
                ldebug(p, RED);
                currentTarget = p;
                shouldShoot = true;
            }
            else if (time() >= (&enemies).Raw()[i].Recorded + 1f)
            {
                ldebug(p, YELLOW);
                currentTarget = p;
            }
            else
            {
                ldebug(p, YELLOW);
            }
        }
    }

    for (int i = (&allies).Length() - 1; i >= 0; i--)
    {
        if (time() >= (&allies).Raw()[i].Expires)
        {
            (&allies).Remove(i);
        }
        else
        {
            float2 p = (&allies).Raw()[i].Position;
            debug(p, GREEN);
        }
    }

    byte[MaxCommandDataSize] _command;

    int commandId = dequeue_command(&_command);

    if (commandId == 1)
    {
        CommandAttackAt* command = &_command as CommandAttackAt*;
        currentTarget = command.position;
        currentDestination = new float2;
        ToLocal(&currentTarget);
    }
    else if (commandId == 2)
    {
        CommandMoveTo* command = &_command as CommandMoveTo*;
        currentTarget = new float2;
        currentDestination = command.position;
    }

    if (currentTarget.x != 0f &&
        currentTarget.y != 0f)
    {
        turretLastUseTime = time();
        if (shouldShoot)
        {
            ShootAt(currentTarget, &targetedPosition);
        }
        else
        {
            TargetAt(currentTarget, &targetedPosition);
        }
    }
    else if (time() - turretLastUseTime > 5f)
    {
        CombatTurret.TurretTargetRotation = 0f;
        CombatTurret.TurretTargetAngle = 0f;
    }

    if (currentDestination.x != 0f &&
        currentDestination.y != 0f)
    {
        MoveTo(currentDestination.x, currentDestination.y);
    }
}
