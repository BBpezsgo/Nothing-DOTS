using "lib/turret.bbc";
using "lib/move.bbc";
using "lib/api.bbc";
using "lib/protocol.bbc";

[UnitCommand(1, "Attack at")]
struct CommandAttackAt
{
    [Context("position3")] float3 position;
}

[UnitCommand(2, "Move to")]
struct CommandMoveTo
{
    [Context("position2")] float2 position;
}

[UnitCommand(3, "Stop")]
struct CommandStop
{
    
}

const int MaxCommandDataSize = 8;

void Receive()
{
    u8[MaxPacketLength] v;
    float3 direction;

    while (1)
    {
        i32 receivedLength = receive(&v, MaxPacketLength, &direction);
        if (!receivedLength) { break; }

        if (v[0] == (u8)REQUEST_HEADER &&
            receivedLength == sizeof(VerificationRequestPacket))
        {
            VerificationRequestPacket* packet = &v as VerificationRequestPacket*;

            VerificationResponsePacket responsePacket = new VerificationResponsePacket;
            responsePacket.Header = (u8)RESPONSE_HEADER;

            Print("I-REQ ");
            Print(packet.Position);
            PrintLine();

            if (DistanceSqr(packet.Position, GPS.Position) < DistanceThresholdSq)
            {
                PrintLine(" M");
                responsePacket.Position = GPS.Position;
                debug(packet.Position, CYAN);
                send(&responsePacket as u8[]*, sizeof(VerificationResponsePacket), &direction);
            }
            else
            {
                for (int i = 0; i < (&allies).Length(); i++)
                {
                    f32 distanceSqr = DistanceSqr((&allies).Raw()[i].Position, packet.Position);
                    if (distanceSqr < DistanceThresholdSq)
                    {
                        PrintLine(" K");
                        IndirectVerificationResponsePacket indirectResponsePacket = new IndirectVerificationResponsePacket;
                        indirectResponsePacket.Header = (u8)INDIRECT_RESPONSE_HEADER;
                        indirectResponsePacket.Position = (&allies).Raw()[i].Position;
                        indirectResponsePacket.Expires = (&allies).Raw()[i].Expires;
                        send(&indirectResponsePacket as u8[]*, sizeof(IndirectVerificationResponsePacket), &direction);
                        return;
                    }
                }
            }
        }
        else if (v[0] == (u8)RESPONSE_HEADER &&
                 receivedLength == sizeof(VerificationResponsePacket))
        {
            VerificationResponsePacket* packet = &v as VerificationResponsePacket*;

            Print("I-RES ");
            Print(packet.Position);
            PrintLine();

            Unit unit;
            unit.Position = packet.Position;
            unit.Recorded = time();
            unit.Expires = time() + 10f;
            unit.VerificationSentAt = time();
            unit.PositionRecords = 1;
            RemoveUnitAt(&enemies, unit.Position);
            UpdateUnit(&allies, unit);
        }
        else if (v[0] == (u8)ENEMY_HEADER &&
                 receivedLength == sizeof(EnemyNotificationPacket))
        {
            EnemyNotificationPacket* packet = &v as EnemyNotificationPacket*;

            Unit unit;
            unit.Position = packet.Position;
            unit.Recorded = 0f;
            unit.Expires = time() + 5f;
            unit.VerificationSentAt = time();
            unit.PositionRecords = 1;

            Print("I-E ");
            PrintLine(unit.Position);
            UpdateUnit(&enemies, unit);
        }
        else if (v[0] == (u8)INDIRECT_RESPONSE_HEADER &&
                 receivedLength == sizeof(IndirectVerificationResponsePacket))
        {
            IndirectVerificationResponsePacket* packet = &v as IndirectVerificationResponsePacket*;

            Print("I-RES ");
            Print(packet.Position);
            PrintLine();

            Unit unit;
            unit.Position = packet.Position;
            unit.Recorded = time();
            unit.Expires = packet.Expires;
            unit.VerificationSentAt = time();
            unit.PositionRecords = 1;
            RemoveUnitAt(&enemies, unit.Position);
            UpdateUnit(&allies, unit);
        }
    }
}

int UnitIndexAt(List<Unit>* units, float3 position)
{
    for (int i = 0; i < units.Length(); i++)
    {
        if (DistanceSqr(units.Raw()[i].Position, position) < DistanceThresholdSq)
        { return i; }
    }
    return -1;
}

int UnitIndexAt(List<Unit>* units, float3 position, float* distanceSqr)
{
    for (int i = 0; i < units.Length(); i++)
    {
        *distanceSqr = DistanceSqr(units.Raw()[i].Position, position);
        if ((*distanceSqr) < DistanceThresholdSq)
        { return i; }
    }
    return -1;
}

void UpdateUnit(List<Unit>* units, Unit unit)
{
    float distanceSqr = 0f;
    int existing = UnitIndexAt(units, unit.Position, &distanceSqr);
    if (existing == -1)
    {
        if (units.Length() >= 4)
        {
            PrintLine("Too many units");
            units.Raw()[0] = unit;
        }
        else
        {
            units.Add(unit);
        }
    }
    elseif (distanceSqr < 2f)
    {
        if (units.Raw()[existing].PositionRecords > 20)
        {
            unit.Position.x = unit.Position.x;
            unit.Position.y = unit.Position.y;
            unit.PositionRecords = 1;
            units[existing] = unit;
        }
        else
        {
            unit.Position.x = (units.Raw()[existing].Position.x * unit.PositionRecords + unit.Position.x) / (float)(unit.PositionRecords + 1);
            unit.Position.y = (units.Raw()[existing].Position.y * unit.PositionRecords + unit.Position.y) / (float)(unit.PositionRecords + 1);
            unit.PositionRecords++;
            units[existing] = unit;
        }
    }
    else
    {
        unit.Position.x = unit.Position.x;
        unit.Position.y = unit.Position.y;
        units[existing] = unit;
    }
}

void RemoveUnitAt(List<Unit>* units, float3 unit)
{
    u8 isNotDone = true;
    for (int i = 0; isNotDone && i < units.Length(); i++)
    {
        f32 distanceSqr = DistanceSqr(units.Raw()[i].Position, unit);
        if (distanceSqr < DistanceThresholdSq)
        {
            isNotDone = false;
            units.SwapBack(i);
        }
    }
}

struct Unit
{
    float3 Position;
    f32 Recorded;
    f32 Expires;
    f32 VerificationSentAt;
    u8 PositionRecords;
}

float3 targetedPosition = new float3;
float3 currentTarget = new float3;
float3 currentUserTarget = new float3;
List<Unit> enemies = new List<Unit>(4);
List<Unit> allies = new List<Unit>(4);
float2 currentDestination = new float2;
u8 shouldShoot = false;
f32 turretLastUseTime = 0f;

PrintLine("Started ...");

while (1)
{
    Receive();

    currentTarget = new float3;
    shouldShoot = false;

    for (int i = (&enemies).Length() - 1; i >= 0; i--)
    {
        if (time() >= (&enemies).Raw()[i].Expires)
        {
            PrintLine("#E-OLD");
            (&enemies).SwapBack(i);
        }
        else
        {
            float3 p = (&enemies).Raw()[i].Position;
            ToLocal(&p);

            float3 norm = Normalised(p);
            Radar.RadarDirection = Atan2(norm.y, norm.x);
            float3 radarPoint = radar();
            bool destroyed = empty(radarPoint) || DistanceSqr(p, radarPoint) > DistanceThresholdSq;

            if (destroyed)
            {
                (&enemies).SwapBack(i);
            }
            elseif (time() >= (&enemies).Raw()[i].Recorded + 4f)
            {
                ToGlobal(&p);
                debug(p, RED);
                currentTarget = p;
                shouldShoot = true;
            }
            else if (time() >= (&enemies).Raw()[i].Recorded + 1f)
            {
                ToGlobal(&p);
                debug(p, YELLOW);
                currentTarget = p;
            }
            else
            {
                ldebug(p, YELLOW);
            }
        }
    }

    for (int i = (&allies).Length() - 1; i >= 0; i--)
    {
        if (time() >= (&allies).Raw()[i].Expires)
        {
            (&allies).SwapBack(i);
        }
        else
        {
            float3 p = (&allies).Raw()[i].Position;
            debug(p, GREEN);
        }
    }

    byte[MaxCommandDataSize] _command;

    int commandId = dequeue_command(&_command);

    if (commandId == 1)
    {
        CommandAttackAt* command = &_command as CommandAttackAt*;
        currentUserTarget = command.position;
        currentDestination = new float3;
    }
    else if (commandId == 2)
    {
        CommandMoveTo* command = &_command as CommandMoveTo*;
        currentUserTarget = new float3;
        currentDestination = command.position;
    }
    else if (commandId == 3)
    {
        currentUserTarget = new float3;
        currentDestination = new float3;
    }

    if (currentUserTarget.x || currentUserTarget.z)
    {
        currentTarget = currentUserTarget;
        shouldShoot = true;
    }

    if (currentTarget.x != 0f &&
        currentTarget.z != 0f)
    {
        turretLastUseTime = time();
        if (shouldShoot)
        {
            ShootAt(currentTarget, &targetedPosition);
        }
        else
        {
            TargetAt(currentTarget, &targetedPosition);
        }
    }
    else if (time() - turretLastUseTime > 5f)
    {
        CombatTurret.TurretTargetRotation = 0f;
        CombatTurret.TurretTargetAngle = 0f;
    }

    if (currentDestination.x != 0f &&
        currentDestination.y != 0f)
    {
        MoveTo(currentDestination.x, currentDestination.y);
    }
    else
    {
        StopMoving();
    }
}
